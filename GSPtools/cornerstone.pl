# Usage: `cornerstone.pl folder locations genome`
# Output: a 'proteins' folder of files with protein sequences
###  folder: work folder and possibly the location of
###     `locations` and genome; it is the
###     place where cornerstone will place the "proteins" folder
### locations: file containing the list of sequence locations
###     generated by Genbank's search "To Text" function;
###     by default, cornerstone will search in the folder you specified
###     for `folder`, which you can override with a full path
### genome: haystack to search (e.g. the _E. coli_ K12 genome);
###     like `locations`, cornerstone will first search in
###     the work folder you specified

use strict;
use warnings;
package Cornerstone;

# find_bounds(filename, block for bounds, block for names)
### This is a generator. It yields beginning and ending
### indices first, then the protein names.
sub find_bounds {
	my ($file, $push_bounds, $push_name) = @_;
	open(my $handle, $file) or
      die "Cornerstone::find_bounds cannot open file \"$file\" for reading\n";
	while(<$handle>) {
		if ($_ =~ /(\d*)\.\.(\d*)/) {
			my ($start, $end) = ($1, $2);         
         # Quirk: Genbank sometimes reverses the indices
			($start, $end) = ($end, $start) if ($start > $end);
         # Leader sequence of 12 before `tac/uag` starts
			&$push_bounds($start, $end + 12);
		} elsif ($_ =~ /^\d*?: (.*)$/) {
			my $hair = $1; # Vivek's hairy
			$hair =~ s/\s//g;
			&$push_name($hair);
		}
	}
}

# Cf. page 43-6 of _Programming Perl for Bioinformatics_
# by James D. Tisdall
sub reverse_complement {
	for(shift) { tr/atcg/uagc/; return scalar reverse; }
}

# The entire genome in a dainty little variable.
our $o;
sub extract {
	my ($start, $end) = @_;
	substr($o, $start, $end - $start + 1);
}

sub main {
   require 'getseq.pl';
   my ($dir, $locations,$genome) = @ARGV;
   chdir($dir);
   
   # Slurp the E. Coli genome as a huge text file
   # for quick access.
   $o = getseq($genome);
   
   my (@sequences, @names);
   # The first anonymous function pushes yielded bounds
   # into the @sequences array after extracting the
   # sequences with `extract`.
   # The second anonymous function pushes it
   # without any processing.
   find_bounds $locations,
      sub { push @sequences, &extract; },
      sub { push @names, shift; };
   
   # Output: store to file with special filenames
   mkdir('proteins'); chdir('proteins');
   map {
      my $name = shift @names; {
         open(my $handle, ">$name.txt") or
            die("Cornerstone cannot open $name for writing");
         print $handle reverse_complement $_;
      }
   } @sequences;
   
   print 'Done', $/;
}

main() if __FILE__ eq $0; 1;